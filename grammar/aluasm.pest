ident = _{ ASCII_ALPHA ~ ASCII_ALPHANUMERIC+ }
bech32 = _{ ASCII_ALPHANUMERIC+ }

COMMENT = { ";" ~ ASCII* }

isae_name = { ".ALU" | ".DYNDATA" | ".BPDIGEST" }
isae = { ".ISAE" ~ isae_name+ }

reg_family = { "a" | "f" | "r" | "s" }
reg_member = { ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
reg_name = @{ reg_family ~ reg_member }
reg_index = !{ "[" ~ ASCII_DIGIT+ ~ "]" }
reg = @{ reg_name ~ reg_index }

lit_num = { lit_dec | lit_hex | lit_oct | lit_bin }
lit_dec = { ASCII_DIGIT+ }
lit_hex = @{ ("0x" | "0X") ~ ASCII_HEX_DIGIT+ }
lit_oct = @{ ("0o" | "0O") ~ ASCII_OCT_DIGIT+ }
lit_bin = @{ ("0b" | "0B") ~ ("1" | "0")+ }
lit_str = @{ "\"" ~ ANY+ ~ "\"" }
lit_chr = @{ "\'" ~ ANY ~ "\'" }
lit = { lit_dec | lit_hex | lit_oct | lit_bin | lit_str | lit_chr }

lib_ident = { ident }
lib_name = { lib_hash | lib_ident }
lib_hash = @{ "alu1" ~ bech32 }
lib_def = { lib_ident ~ lib_hash }
libs = {  ".LIBS" ~ lib_def+ }

offset_rel = { "+" | "-" | "=" }
offset = { offset_rel? ~ lit_num }

goto_name = @{ ident }
goto_offset = _{ offset | goto_name }
goto_ref = _{ "@" ~ lib_name }
goto = @{ goto_offset ~ goto_ref? }

label = @{ ident ~ ":" }
op = @{ ident ~ flags? }
flags = _{ ":" ~ flag+ }
flag = { ASCII_ALPHA }
operand = { reg | lit | goto | var_name }
instruction = { label? ~ op ~ flags? ~ operand+ }

routine_main = { ".MAIN" }
routine_name = { routine_main | ident }
routine_decl = { ".ROUTINE" ~ routine_name }
routine = { routine_decl ~ instruction+ }
code = { routine+ }

var_name = @{ "$" ~ ident }
const_decl = { var_name ~ lit }
data = { ".DATA" ~ const_decl+ }

dyn_decl = { var_name ~ lit_str }
dyn = { ".DYN" ~ dyn_decl+ }

segment = { isae | code | libs | data | dyn }
program = _{ segment+ }
