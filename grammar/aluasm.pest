ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")+ }
bech32 = _{ ASCII_ALPHANUMERIC+ }

COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* }
WHITESPACE = _{ " " | "\t" }

isae_alu = { "ALU" }
isae_dyn = { "ALURE" }
isae_bpdigest = { "BPDIGEST" }
isae_bitcoin = { "BP" }
isae_rgb = { "RGB" }
isae_unknown = @{ ASCII_ALPHA_UPPER+ }
isae_name = { isae_alu | isae_dyn | isae_bpdigest | isae_bitcoin | isae_rgb }
isae = { ".ISAE" ~ (isae_name | isae_unknown | NEWLINE)+ }

reg_family = { "a" | "f" | "r" | "s" }
reg_member = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
reg_name = @{ reg_family ~ reg_member }
reg_index = @{ ASCII_DIGIT+ }
reg_sub = _{ "[" ~ reg_index ~ "]" }
reg = @{ reg_name ~ reg_sub }

lit_float = @{ (ASCII_DIGIT* ~ "." ~ ASCII_DIGIT+) | (ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT*) }
lit_dec = @{ ASCII_DIGIT+ }
lit_hex = @{ ("0x" | "0X") ~ ASCII_HEX_DIGIT+ }
lit_oct = @{ ("0o" | "0O") ~ ASCII_OCT_DIGIT+ }
lit_bin = @{ ("0b" | "0B") ~ ("1" | "0")+ }
lit_str = @{ "\"" ~ (!(NEWLINE | "\"") ~ ANY)* ~ "\"" }
lit_chr = @{ "\'" ~ ANY ~ "\'" }
lit_num = { lit_dec | lit_hex | lit_oct | lit_bin }
lit = { lit_float | lit_hex | lit_oct | lit_bin | lit_dec | lit_str | lit_chr }

lib_ident = { ident }
lib_bech = @{ ^"alu1" ~ bech32 }
lib_name = { lib_bech | lib_ident }
lib_def = { lib_ident ~ lib_bech ~ NEWLINE+ }
libs = {  ".LIBS" ~ NEWLINE* ~ lib_def* }

goto_rel = { "+" | "-" | "=" }
goto_offset = @{ goto_rel ~ lit_num }
goto = { ident | goto_offset }
call = { lib_name? ~ "->" ~ ident }

flag = { ASCII_ALPHA }
op = @{ ident ~ ("." ~ flag+)? }
operand = { reg | call | lit | var_name | goto }
label = @{ ident ~ ":" }
instruction = { label? ~ NEWLINE* ~ op ~ (operand ~ ",")* ~ operand? ~ NEWLINE+ }

routine_name = { ident }
routine_main = { ".MAIN" ~ NEWLINE* }
routine_decl = { ".ROUTINE" ~ routine_name ~ NEWLINE* }
routine = { (routine_decl | routine_main) ~ instruction* }
code = { routine+ }

var_name = @{ "$" ~ ident }
const_decl = { var_name ~ "=" ~ lit ~ NEWLINE+ }
data = { ".CONST" ~ NEWLINE* ~ const_decl* }

dyn_decl = { var_name ~ lit_str ~ NEWLINE+ }
dyn = { ".DYN" ~ NEWLINE* ~ dyn_decl* }

segment = _{ isae | code | libs | data | dyn }
program = { SOI ~ segment+ ~ EOI }
