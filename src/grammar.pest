ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")+ }
bech32 = _{ ASCII_ALPHANUMERIC+ }

COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* }
WHITESPACE = _{ " " | "\t" }

isae_name = { ASCII_ALPHA_UPPER+ }
isae = { ".ISAE" ~ (isae_name | NEWLINE)+ }

reg_family = { "a" | "f" | "r" | "s" }
reg_member = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
reg_name = @{ reg_family ~ reg_member }
reg_index = @{ ASCII_DIGIT+ }
reg_sub = _{ "[" ~ reg_index ~ "]" }
reg = @{ reg_name ~ reg_sub }

lit_float = @{ (ASCII_DIGIT* ~ "." ~ ASCII_DIGIT+) | (ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT*) }
lit_dec = @{ ASCII_DIGIT+ }
lit_hex = @{ ("0x" | "0X") ~ ASCII_HEX_DIGIT+ }
lit_oct = @{ ("0o" | "0O") ~ ASCII_OCT_DIGIT+ }
lit_bin = @{ ("0b" | "0B") ~ ("1" | "0")+ }
lit_str = @{ "\"" ~ (!(NEWLINE | "\"") ~ ANY)* ~ "\"" }
lit_chr = @{ "\'" ~ ANY ~ "\'" }
lit_num = { lit_dec | lit_hex | lit_oct | lit_bin }
lit = { lit_float | lit_hex | lit_oct | lit_bin | lit_dec | lit_str | lit_chr }

lib_ident = { ident }
lib_bech = @{ ^"alu1" ~ bech32 }
lib_name = { lib_bech | lib_ident }
lib_def = { lib_ident ~ lib_bech ~ NEWLINE+ }
libs = {  ".LIBS" ~ NEWLINE* ~ lib_def* }

goto_rel = { "+" | "-" | "=" }
goto_offset = { goto_rel ~ lit_num }
goto_name = { ident }
goto = { goto_name | goto_offset }
call_name = { ident }
call = { lib_name? ~ "->" ~ call_name }

flag = { ASCII_ALPHA }
flags = ${"." ~ flag+}
op = { ident }
operand = { reg | call | lit | var | goto }
label = { ident }
instruction = { (label ~ ":")? ~ NEWLINE* ~ op ~ flags? ~ (operand ~ ",")* ~ operand? ~ NEWLINE+ }

routine_name = { ident }
routine_main = { ".MAIN" ~ NEWLINE* }
routine_decl = { ".ROUTINE" ~ routine_name ~ NEWLINE* }
routine = { (routine_decl | routine_main) ~ instruction* }

var_name = { ident }
var = ${ "$" ~ var_name }

const_decl = { var_name ~ "=" ~ lit ~ NEWLINE+ }
data = { ".CONST" ~ NEWLINE* ~ const_decl* }

input_name = { lit_str }
input_decl = { var ~ input_name ~ NEWLINE+ }
input = { ".INPUT" ~ NEWLINE* ~ input_decl+ }

segment = _{ isae | routine | libs | data | input }
program = { SOI ~ segment+ ~ EOI }
